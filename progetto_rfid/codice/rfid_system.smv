MODULE main

VAR
 r : reader(t1,t2,t3,t4);
 t1: tag(r,0ub8_0000_0000,2);
 t2: tag(r,0ub8_0000_0001,2);
 t3: tag(r,0ub8_0000_0010,2);
 t4: tag(r,0ub8_0000_0011,2);

-- verifichiamo se tutti gli stati dei tag
-- sono raggiungibili

/--CTLSPEC
EF(t3.state=single)
CTLSPEC
EF(t3.state=end)--/

-- check on reader
CTLSPEC
EF(r.b=0ub2_10)
CTLSPEC
EF(r.a=0ub1_0)
CTLSPEC
EF(r.b=0ub2_00 & t1.value=two & t1.state=transmit)
CTLSPEC
EF(r.a=0ub1_0 & t3.value=one & t3.state=transmit)
CTLSPEC
EF(r.bit=2 & r.state=single)
CTLSPEC
EF(r.bit=2 & r.state=single & r.b=0ub2_10)


-- verifichiamo se il reader rimane in deadlock
-- in una particolare configurazione

MODULE reader(t1,t2,t3,t4)

VAR
 continue: boolean;
 bit     : -1..8;
 a       : unsigned word[1];
 b       : unsigned word[2];
 state   : {transmit,receive,idle,collision_bit,
            collision,single_bit,single,end};


-- collision detection
DEFINE 
  k := count((t1.state=transmit),(t2.state=transmit),
             (t3.state=transmit),(t4.state=transmit)); 

ASSIGN

    init(continue) := TRUE;
    next(continue) := 
        case
           state=end : FALSE;
           TRUE : continue;
        esac;

    init(a) := 0ub1_0;
    next(a) := 
        case
           (bit=1) & (state=collision)                     : 0ub1_0;
           (bit=2) & (state=single) & (resize(b,1)=0ub1_1) : 0ub1_1;
           (bit=1) & (state=single) & (a=0ub1_0)           : 0ub1_1;
           TRUE : a;
        esac;
    
    init(b) := 0ub2_00;
    next(b) := 
        case
           (bit=2) & (state=collision)                     : a :: 0ub1_0;
           (bit=2) & (state=single) & (resize(b,1)=0ub1_0) : resize(b,1) :: 0ub1_1;
           TRUE : b;
        esac;

    init(bit) := 0;
    next(bit) :=
        case
          -- collision
          (state=collision_bit) & (bit=0)                     :  1;
          (state=collision_bit) & (bit=1)                     :  2; 

          -- single
          (state=single_bit) & (bit=1) & (a=0ub1_1)           : -1;
          (state=single_bit) & (bit=1) & (a=0ub1_0)           :  1;
          (state=single_bit) & (bit=2) & (resize(b,1)=0ub1_1) :  1;
          (state=single_bit) & (bit=2) & (resize(b,1)=0ub1_0) :  2;
 
          TRUE : bit;
        esac;


    init(state) := transmit;
    next(state) :=
        case
          (continue=TRUE) & (state=transmit)                : receive;
          (k=0) & (state=receive)                           : idle;
          (k>=2) & ((state=receive) | (state=idle))         : collision_bit;
          (k=1) & ((state=receive) | (state=idle))          : single_bit;
          (state=collision_bit)                             : collision;
          (state=single_bit)                                : single;
          (bit=-1) & ((state=idle) | (state=single))        : end;
          (state=idle) | (state=collision) | (state=single) : transmit;
          TRUE : state;
        esac;


MODULE tag(r,address,significant_bit)

VAR
 continue : boolean;
 length   : boolean;
 value    : {alt, empty,one,two};
 state    : {receive,equal,equal_1,transmit,collision,single,end};
 

ASSIGN

    init(continue) := TRUE;
    next(continue) := 
        case
           state=end : FALSE;
           TRUE : continue;
        esac;

    init(length) := FALSE;
    next(length) := 
        case
           (r.state=receive) & (significant_bit >= r.bit) : TRUE;
           TRUE : length;
        esac; 

    init(value) := alt;
    next(value) :=
        case
           (state=equal) & (r.bit=0) : empty;
           (state=equal) & (r.bit=1) : one;
           (state=equal) & (r.bit=2) : two;
           TRUE : value;
        esac;

    init(state) := receive;
    next(state) :=
        case
           (r.state=receive) & (length) & (state=receive) : equal;
           (state=equal)                                  : equal_1;
           (state=equal_1) & ((value=empty) | 
           ((value=one) & (r.a=resize(address,1))) |
           ((value=two) & (r.b=resize(address,2))))       : transmit;
           (r.state=collision) & (state=transmit)         : collision;
           (r.state=single) & (state=transmit)            : single;
           (state=equal_1) | (state=collision)            : receive;
           (state=single)                                 : end;
           TRUE : state;
        esac;



    



 
